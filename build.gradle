buildscript {
	ext {
		springBootVersion = '1.5.9.RELEASE'
	}
	repositories {
		mavenCentral()
		mavenLocal()
		jcenter()
		maven { url "http://repo.spring.io/snapshot" }
		maven { url "http://repo.spring.io/milestone" }
		maven { url "http://repo.spring.io/release" }
	}
	dependencies {
		classpath "org.springframework.boot:spring-boot-gradle-plugin:1.5.3.RELEASE"
		classpath 'org.asciidoctor:asciidoctor-gradle-plugin:1.5.3'
		classpath 'org.asciidoctor:asciidoctorj-epub3:1.5.0-alpha.6'
		classpath 'org.asciidoctor:asciidoctorj-pdf:1.5.0-alpha.15'
		classpath "org.springframework.cloud:spring-cloud-contract-gradle-plugin:${project.hasProperty("scContractVersion") ? project.property("scContractVersion") : "1.1.1.RELEASE"}"
	}
}

plugins {
	id 'org.springframework.boot' version '1.5.9.RELEASE'
	id 'org.asciidoctor.convert' version '1.5.3'
}

apply plugin: 'groovy'
apply plugin: 'org.springframework.boot'
apply plugin: 'spring-cloud-contract'
apply plugin: 'maven-publish'
apply plugin: 'org.asciidoctor.convert'
apply from: 'gradle/pipeline.gradle'
apply plugin: "jacoco"

group = 'com.example.github'
version = getProp('newVersion') ?: '0.0.1-SNAPSHOT'


ext {
	projectGroupId = project.group
	projectArtifactId = project.name
	projectVersion = project.version
	stubrunnerIds = 'com.example.github:github-webhook'
}

repositories {
	mavenCentral()
	mavenLocal()
	if (getProp("M2_LOCAL")) {
		maven {
			url getProp("M2_LOCAL")
		}
	}
	maven { url "http://repo.spring.io/snapshot" }
	maven { url "http://repo.spring.io/milestone" }
	maven { url "http://repo.spring.io/release" }
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:$BOM_VERSION"
	}
}

if (gradle.startParameter.taskRequests.any { it.args.contains("apiCompatibility") }) {
	contracts {
		baseClassForTests = 'org.springframework.github.BaseClass'
		basePackageForTests = 'com.example.contracttests'
		// REPO_WITH_JARS is deprecated
		contractsRepositoryUrl = getProp('REPO_WITH_JARS') ?: getProp('REPO_WITH_BINARIES') ?: 'http://localhost:8081/artifactory/libs-release-local'
		contractsPath = "/"
		contractDependency {
			groupId = project.group
			artifactId = project.name
			delegate.classifier = "stubs"
			delegate.version = getProp("latestProductionVersion")
		}
	}
} else {
	contracts {
		baseClassForTests = 'org.springframework.github.BaseClass'
		basePackageForTests = 'com.example.contracttests'
	}
}

dependencies {
	compile ('org.projectlombok:lombok')
	compile ('org.springframework.analytics:spring-analytics:1.0.0.RELEASE')
	compile ('org.springframework.cloud:spring-cloud-starter-eureka')
	compile ('com.toomuchcoding.jsonassert:jsonassert')
	compile ('org.hibernate:hibernate-validator')
	compile ('org.springframework.cloud:spring-cloud-starter-stream-rabbit')

	// API Framework Needs:
	compile('io.springfox:springfox-swagger2:2.4.0')
	compile('io.springfox:springfox-swagger-ui:2.4.0')

	//devOps needs
	compile('org.springframework.boot:spring-boot-devtools')
	compile ('org.springframework.boot:spring-boot-starter-actuator')
	compile('org.springframework.boot:spring-boot-actuator-docs')

	compile ('org.flywaydb:flyway-core')
	compile ('org.springframework.boot:spring-boot-starter-data-jpa')
	compile ('org.springframework.boot:spring-boot-starter-web')
	compile ('com.h2database:h2')
	compile ('mysql:mysql-connector-java')
	compile ('io.micrometer:micrometer-spring-legacy:1.0.0-rc.1')
	compile ('io.micrometer:micrometer-registry-prometheus:1.0.0-rc.1')

	testCompile ('org.springframework.cloud:spring-cloud-starter-contract-stub-runner')
	testCompile ('org.springframework.cloud:spring-cloud-stream-test-support')
	testCompile ('com.googlecode.junit-toolbox:junit-toolbox:2.2')
	testCompile ('org.springframework.cloud:spring-cloud-starter-contract-verifier')
	testCompile ('com.jayway.restassured:spring-mock-mvc')
	testCompile('org.springframework.restdocs:spring-restdocs-mockmvc')
	testCompile ('org.awaitility:awaitility:3.0.0')
}

publishing {
	repositories {
		maven {
			// REPO_WITH_JARS is deprecated
			url getProp('REPO_WITH_JARS') ?: getProp('REPO_WITH_BINARIES') ?: 'http://localhost:8081/artifactory/libs-release-local'
			credentials {
				username getProp('M2_SETTINGS_REPO_USERNAME') ?: 'admin'
				password getProp('M2_SETTINGS_REPO_PASSWORD') ?: 'password'
			}
		}
	}
	publications {
		mavenJava(MavenPublication) {
			artifactId project.name
			from components.java
		}
	}
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.5'
}

String getProp(String propName) {
	return hasProperty(propName) ?
			(getProperty(propName) ?: System.properties[propName]) : System.properties[propName] ?:
			System.getenv(propName)
}

asciidoctor {
	backends 'html5', 'pdf', 'epub3', 'docbook'
	attributes \
        'build-gradle': file('build.gradle'),
			//'sourcedir': project.sourceSets.main.java.srcDirs[0],
			'endpoint-url': 'http://example.org',
			'source-highlighter': 'coderay',
			'imagesdir': './images',
			'toc': 'left',
			'icons': 'font',
			'setanchors': '',
			'idprefix': '',
			'idseparator': '-',
			'docinfo1': ''
}

test {
	exclude '**/smoke/*'
	exclude '**/e2e/*'
	exclude 'E2ETestSuite'
}

jacocoTestReport{
	additionalSourceDirs = files(sourceSets.main.allJava.srcDirs)
	reports {
		xml.enabled false
		csv.enabled false
		html.destination "${buildDir}/reports/jacoco/html"
	}
	executionData = files('build/jacoco/test.exec')
}

test.finalizedBy jacocoTestReport,asciidoctor

